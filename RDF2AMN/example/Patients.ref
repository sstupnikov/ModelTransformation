/* Patients
 * Author: ssa
 * Creation date: 5/25/2022
 */

REFINEMENT Patients
REFINES HospitalRef

SEES ContextRDF


DEFINITIONS
    STRING_TYPE == seq(0..255)

ABSTRACT_VARIABLES
    
    tbl_patient
    

INVARIANT

    tbl_patient: POW(
        struct(patientid: INT, name: STRING_TYPE, type: BOOL, stage: INT)) &
    
    // Mappings
    // Mapping 1a: Patient
    !(patient, patientid, name).(        
        patient: tbl_patient &
        patientid: INT & patient'patientid = patientid & 
        name: STRING_TYPE &  patient'name = name        
        =>
        #patientInd.(patientInd: ICEXT(Patient) & 
            IS_IR(personIRI(patientid)) = patientInd &
            (patientInd |-> name): IEXT_String_Range(hasName)
        )
    ) &
    // Mapping 2a: Neoplasm
    !(patient, patientid).(
        patient: tbl_patient & 
        patientid: INT & patient'patientid = patientid 
        =>
        #(patientInd, neoplasmInd).(
            patientInd: ICEXT(Patient) & IS_IR(personIRI(patientid)) = patientInd &
            neoplasmInd: ICEXT(Neoplasm) & IS_IR(neoplasmIRI(patientid)) = neoplasmInd &
            (patientInd |-> neoplasmInd): IEXT(hasNeoplasm)
        )
    ) &
    // Mapping 3a: NSCLC
    !(patient, patientid).(
        patient: tbl_patient & 
        patientid: INT & patient'patientid = patientid & 
        patient'type = FALSE 
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(NSCLC) &
            IS_IR(neoplasmIRI(patientid)) = neoplasmInd            
        )
    ) &   
    // Mapping 4a: SCLC
    !(patient, patientid).(
        patient: tbl_patient & 
        patientid: INT & patient'patientid = patientid & 
        patient'type = TRUE 
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(SCLC) & 
            IS_IR(neoplasmIRI(patientid)) = neoplasmInd 
        )
    ) &
    // Mapping 5a: Stage IIIa
    !(patient, patientid).(
        patient: tbl_patient & 
        patientid: INT & patient'patientid = patientid & 
        patient'stage = 4 &  
        patient'type = FALSE  
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(NSCLC) & 
            IS_IR(neoplasmIRI(patientid)) = neoplasmInd &
            (neoplasmInd |-> stageIIIa): IEXT_CancerStage_Range(hasStage)
        )
    )    
 
INITIALISATION 
    
    tbl_patient:= {}

OPERATIONS
res <--  patientsWithTumorOfStageIIIa =
BEGIN
    res:= 
    { rcrd | 
        rcrd: struct(name: STRING_TYPE) & 
        #ptnt.(ptnt: tbl_patient & ptnt'type = FALSE & ptnt'stage = 4 )
    }          
END

END