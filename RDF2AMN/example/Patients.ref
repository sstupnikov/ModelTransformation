/* Patients
 * Author: ssa
 * Creation date: 5/25/2022
 */

REFINEMENT Patients
REFINES Hospital

SEES ContextRDF


DEFINITIONS
    STRING_TYPE == seq(0..255)

ABSTRACT_CONSTANTS
    personIRI,
    neoplasmIRI

PROPERTIES
    personIRI: 1..NN  >-> IRI & ran(personIRI) <: dom(IS_IR) &
    neoplasmIRI: 1..NN >-> IRI & ran(neoplasmIRI) <: dom(IS_IR) &
    ran(personIRI) /\ ran(neoplasmIRI) = {}


ABSTRACT_VARIABLES
    
    tbl_patient
    

INVARIANT

    tbl_patient: POW(
        struct(patientid: INT, name: STRING_TYPE, type: BOOL, stage: INT)) &
    !(pt1, pt2).(pt1: tbl_patient & pt2: tbl_patient & pt1 /= pt2 => 
        pt1'patientid /= pt2'patientid) &
    
    // Mappings
    // Mapping 1a: Patient
    !(patient).(        
        patient: tbl_patient =>
        #patientInd.(patientInd: ICEXT(Patient) & 
            IS_IR(personIRI(patient'patientid)) = patientInd &
            (patientInd |-> patient'name): IEXT_String_Range(hasName)
        )
    ) &
    // Mapping 2a: Neoplasm
    !(patient).(
        patient: tbl_patient
        =>
        #(patientInd, neoplasmInd).(
            patientInd: ICEXT(Patient) & IS_IR(personIRI(patient'patientid)) = patientInd &
            (patientInd |-> neoplasmInd): IEXT(hasNeoplasm) &
            neoplasmInd: ICEXT(Neoplasm) & IS_IR(neoplasmIRI(patient'patientid)) = neoplasmInd 
            
        )
    ) &
    // Mapping 3a: NSCLC
    !(patient).(
        patient: tbl_patient & 
        patient'type = FALSE 
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(NSCLC) &
            IS_IR(neoplasmIRI(patient'patientid)) = neoplasmInd            
        )
    ) &   
    // Mapping 4a: SCLC
    !(patient).(
        patient: tbl_patient & 
        patient'type = TRUE 
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(SCLC) & 
            IS_IR(neoplasmIRI(patient'patientid)) = neoplasmInd 
        )
    ) &
    // Mapping 5a: Stage IIIa
    !(patient).(
        patient: tbl_patient & 
        patient'stage = 4 &  
        patient'type = FALSE  
        =>
        #(neoplasmInd).(
            neoplasmInd: ICEXT(NSCLC) & 
            IS_IR(neoplasmIRI(patient'patientid)) = neoplasmInd &
            (neoplasmInd |-> stageIIIa): IEXT_CancerStage_Range(hasStage)
        )
    )    
 
INITIALISATION 
    
    tbl_patient:= {}

OPERATIONS
res <--  patientsWithTumorOfStageIIIa =
BEGIN
    res:= 
    { rcrd | 
        rcrd: struct(name: STRING_TYPE) & 
        #ptnt.(ptnt: tbl_patient & ptnt'type = FALSE & ptnt'stage = 4 &
            ptnt'name = rcrd'name)
    }          
END

END